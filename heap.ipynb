{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "consecutive-final",
   "metadata": {},
   "outputs": [],
   "source": [
    "import pytest\n",
    "import numpy as np\n",
    "from matplotlib import pyplot as plt\n",
    "\n",
    "plt.rcParams['figure.figsize'] = [12, 12]\n",
    "plt.rcParams['figure.dpi'] = 100"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "valuable-toyota",
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext cython"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "saved-newfoundland",
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "%%cython -f --compile-args=-fopenmp --link-args=-fopenmp --annotate\n",
    "# cython: profile=False\n",
    "# cython: cdivision=True\n",
    "# cython: boundscheck=False\n",
    "# cython: wraparound=False\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "from libc.stdlib cimport malloc, calloc, free\n",
    "from libc.stdio cimport printf\n",
    "from cython.parallel cimport prange, parallel\n",
    "from cython cimport double, int\n",
    "\n",
    "# As of now the best size found by tuning\n",
    "DEF CHUNK_SIZE = 4096\n",
    "DEF N_THREADS = 4\n",
    "DEF DEBUG = False\n",
    "DEF INF = 1e19\n",
    "\n",
    "# Taken from typedefs.{pxd,pyx}\n",
    "ITYPE = np.intp\n",
    "DTYPE = np.float64\n",
    "\n",
    "ctypedef np.float64_t DTYPE_t\n",
    "ctypedef np.intp_t ITYPE_t\n",
    "\n",
    "# Adapted from:\n",
    "# https://github.com/scikit-learn/scikit-learn/blob/e4bb9fa86b0df873ad750b6d59090843d9d23d50/sklearn/neighbors/_binary_tree.pxi#L513\n",
    "cdef class NeighborsHeap:\n",
    "    \"\"\"A max-heap structure to keep track of distances/indices of neighbors\n",
    "\n",
    "    This implements an efficient pre-allocated set of fixed-size heaps\n",
    "    for chasing neighbors, holding both an index and a distance.\n",
    "    When any row of the heap is full, adding an additional point will push\n",
    "    the furthest point off the heap.\n",
    "\n",
    "    Parameters\n",
    "    ----------\n",
    "    n_pts : int\n",
    "        the number of heaps to use\n",
    "    n_nbrs : int\n",
    "        the size of each heap.\n",
    "    \"\"\"    \n",
    "    cdef DTYPE_t[:, ::1] distances\n",
    "    cdef ITYPE_t[:, ::1] indices\n",
    "\n",
    "    def __cinit__(self):\n",
    "        self.distances = np.zeros((1, 1), dtype=DTYPE, order='C')\n",
    "        self.indices = np.zeros((1, 1), dtype=ITYPE, order='C')\n",
    "\n",
    "    def __init__(self, int n_pts, int n_nbrs):\n",
    "        self.distances = np.full((n_pts, n_nbrs),\n",
    "                                 np.inf,\n",
    "                                 dtype=DTYPE,\n",
    "                                 order='C')\n",
    "        \n",
    "        self.indices = np.zeros((n_pts, n_nbrs), dtype=ITYPE, order='C')\n",
    "\n",
    "    def get_arrays(self, sort=True):\n",
    "        \"\"\"Get the arrays of distances and indices within the heap.\n",
    "\n",
    "        If sort=True, then simultaneously sort the indices and distances,\n",
    "        so the closer points are listed first.\n",
    "        \"\"\"\n",
    "        if sort:\n",
    "            self._sort()\n",
    "        return np.asarray(self.distances), np.asarray(self.indices)\n",
    "\n",
    "    cdef inline double largest(self, int row) nogil except -1:\n",
    "        \"\"\"Return the largest distance in the given row\"\"\"\n",
    "        return self.distances[row, 0]\n",
    "\n",
    "    def push(self, ITYPE_t row, DTYPE_t val, ITYPE_t i_val):\n",
    "        return self._push(row, val, i_val)\n",
    "\n",
    "    cdef int _push(\n",
    "        self,\n",
    "        ITYPE_t row,\n",
    "        DTYPE_t val,\n",
    "        ITYPE_t i_val,\n",
    "    ) nogil except -1:\n",
    "        \"\"\"push (val, i_val) into the given row\"\"\"\n",
    "        cdef:\n",
    "            ITYPE_t i, ic1, ic2, i_swap\n",
    "            ITYPE_t size = self.distances.shape[1]\n",
    "            DTYPE_t* dist_arr = &self.distances[row, 0]\n",
    "            ITYPE_t* ind_arr = &self.indices[row, 0]\n",
    "\n",
    "        # check if val should be in heap\n",
    "        if val > dist_arr[0]:\n",
    "            return 0\n",
    "\n",
    "        # insert val at position zero\n",
    "        dist_arr[0] = val\n",
    "        ind_arr[0] = i_val\n",
    "\n",
    "        # descend the heap, swapping values until the max heap criterion is met\n",
    "        i = 0\n",
    "        while True:\n",
    "            ic1 = 2 * i + 1\n",
    "            ic2 = ic1 + 1\n",
    "\n",
    "            if ic1 >= size:\n",
    "                break\n",
    "            elif ic2 >= size:\n",
    "                if dist_arr[ic1] > val:\n",
    "                    i_swap = ic1\n",
    "                else:\n",
    "                    break\n",
    "            elif dist_arr[ic1] >= dist_arr[ic2]:\n",
    "                if val < dist_arr[ic1]:\n",
    "                    i_swap = ic1\n",
    "                else:\n",
    "                    break\n",
    "            else:\n",
    "                if val < dist_arr[ic2]:\n",
    "                    i_swap = ic2\n",
    "                else:\n",
    "                    break\n",
    "\n",
    "            dist_arr[i] = dist_arr[i_swap]\n",
    "            ind_arr[i] = ind_arr[i_swap]\n",
    "\n",
    "            i = i_swap\n",
    "\n",
    "        dist_arr[i] = val\n",
    "        ind_arr[i] = i_val\n",
    "\n",
    "        return 0\n",
    "\n",
    "    cdef ITYPE_t _sort(self) except -1:\n",
    "        \"\"\"simultaneously sort the distances and indices\"\"\"\n",
    "        cdef:\n",
    "            DTYPE_t[:, ::1] distances = self.distances\n",
    "            ITYPE_t[:, ::1] indices = self.indices\n",
    "            ITYPE_t row\n",
    "            \n",
    "        for row in range(distances.shape[0]):\n",
    "            _simultaneous_sort(&distances[row, 0],\n",
    "                               &indices[row, 0],\n",
    "                               distances.shape[1])\n",
    "        return 0\n",
    "\n",
    "cdef inline void dual_swap(\n",
    "    DTYPE_t* darr,\n",
    "    ITYPE_t* iarr,\n",
    "    ITYPE_t i1,\n",
    "    ITYPE_t i2\n",
    ") nogil:\n",
    "    \"\"\"swap the values at inex i1 and i2 of both darr and iarr\"\"\"\n",
    "    cdef:\n",
    "        DTYPE_t dtmp = darr[i1]\n",
    "        ITYPE_t itmp = iarr[i1]\n",
    "    \n",
    "    darr[i1] = darr[i2]\n",
    "    darr[i2] = dtmp\n",
    "\n",
    "    iarr[i1] = iarr[i2]\n",
    "    iarr[i2] = itmp\n",
    "\n",
    "    \n",
    "cdef int _simultaneous_sort(\n",
    "    DTYPE_t* dist,\n",
    "    ITYPE_t* idx,\n",
    "    ITYPE_t size\n",
    ") nogil except -1:\n",
    "    \"\"\"\n",
    "    Perform a recursive quicksort on the dist array, simultaneously\n",
    "    performing the same swaps on the idx array.  The equivalent in\n",
    "    numpy (though quite a bit slower) is\n",
    "\n",
    "    def simultaneous_sort(dist, idx):\n",
    "        i = np.argsort(dist)\n",
    "        return dist[i], idx[i]\n",
    "    \"\"\"\n",
    "    cdef:\n",
    "        ITYPE_t pivot_idx, i, store_idx\n",
    "        DTYPE_t pivot_val\n",
    "\n",
    "    # in the small-array case, do things efficiently\n",
    "    if size <= 1:\n",
    "        pass\n",
    "    elif size == 2:\n",
    "        if dist[0] > dist[1]:\n",
    "            dual_swap(dist, idx, 0, 1)\n",
    "    elif size == 3:\n",
    "        if dist[0] > dist[1]:\n",
    "            dual_swap(dist, idx, 0, 1)\n",
    "        if dist[1] > dist[2]:\n",
    "            dual_swap(dist, idx, 1, 2)\n",
    "            if dist[0] > dist[1]:\n",
    "                dual_swap(dist, idx, 0, 1)\n",
    "    else:\n",
    "        # Determine the pivot using the median-of-three rule.\n",
    "        # The smallest of the three is moved to the beginning of the array,\n",
    "        # the middle (the pivot value) is moved to the end, and the largest\n",
    "        # is moved to the pivot index.\n",
    "        pivot_idx = size / 2\n",
    "        if dist[0] > dist[size - 1]:\n",
    "            dual_swap(dist, idx, 0, size - 1)\n",
    "        if dist[size - 1] > dist[pivot_idx]:\n",
    "            dual_swap(dist, idx, size - 1, pivot_idx)\n",
    "            if dist[0] > dist[size - 1]:\n",
    "                dual_swap(dist, idx, 0, size - 1)\n",
    "        pivot_val = dist[size - 1]\n",
    "\n",
    "        # partition indices about pivot.  At the end of this operation,\n",
    "        # pivot_idx will contain the pivot value, everything to the left\n",
    "        # will be smaller, and everything to the right will be larger.\n",
    "        store_idx = 0\n",
    "        for i in range(size - 1):\n",
    "            if dist[i] < pivot_val:\n",
    "                dual_swap(dist, idx, i, store_idx)\n",
    "                store_idx += 1\n",
    "        dual_swap(dist, idx, store_idx, size - 1)\n",
    "        pivot_idx = store_idx\n",
    "\n",
    "        # recursively sort each side of the pivot\n",
    "        if pivot_idx > 1:\n",
    "            _simultaneous_sort(dist, idx, pivot_idx)\n",
    "        if pivot_idx + 2 < size:\n",
    "            _simultaneous_sort(dist + pivot_idx + 1,\n",
    "                               idx + pivot_idx + 1,\n",
    "                               size - pivot_idx - 1)\n",
    "    return 0"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "supported-cedar",
   "metadata": {},
   "outputs": [],
   "source": [
    "a = NeighborsHeap(10, 20)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "marine-darwin",
   "metadata": {},
   "outputs": [],
   "source": [
    "a"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "orange-commitment",
   "metadata": {},
   "outputs": [],
   "source": [
    "a.get_arrays()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "injured-alert",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
