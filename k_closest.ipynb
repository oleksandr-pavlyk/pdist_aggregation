{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "consecutive-final",
   "metadata": {},
   "outputs": [],
   "source": [
    "import pytest\n",
    "import numpy as np\n",
    "import itertools\n",
    "from matplotlib import pyplot as plt\n",
    "from sklearn.neighbors import NearestNeighbors\n",
    "from sklearnex import patch_sklearn, unpatch_sklearn\n",
    "\n",
    "plt.rcParams['figure.figsize'] = [12, 12]\n",
    "plt.rcParams['figure.dpi'] = 100"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "valuable-toyota",
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext cython"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "saved-newfoundland",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "%%cython -f --compile-args=-fopenmp --link-args=-fopenmp --annotate\n",
    "# cython: profile=False\n",
    "# cython: cdivision=True\n",
    "# cython: boundscheck=False\n",
    "# cython: wraparound=False\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "from libc.stdlib cimport malloc, calloc, free\n",
    "from libc.string cimport memset\n",
    "from libc.stdio cimport printf \n",
    "from cython.parallel cimport prange, parallel\n",
    "from cython cimport floating, integral\n",
    "\n",
    "# As of now the best size found by tuning\n",
    "DEF CHUNK_SIZE = 4096\n",
    "DEF N_THREADS = 4\n",
    "DEF DEBUG = False\n",
    "DEF INF = 1e19\n",
    "\n",
    "from sklearn.utils._cython_blas cimport _gemm, BLAS_Order, BLAS_Trans\n",
    "from sklearn.utils._cython_blas cimport ColMajor, RowMajor, Trans, NoTrans\n",
    "\n",
    "cdef int _push(\n",
    "    floating* dist,\n",
    "    integral* idx,\n",
    "    integral size,\n",
    "    floating val,\n",
    "    integral i_val,\n",
    ") nogil except -1:\n",
    "    \"\"\"push (val, i_val) into the heap (dist, idx) of the given size\"\"\"\n",
    "    cdef:\n",
    "        integral i, ic1, ic2, i_swap\n",
    "\n",
    "    # check if val should be in heap\n",
    "    if val > dist[0]:\n",
    "        return 0\n",
    "\n",
    "    # insert val at position zero\n",
    "    dist[0] = val\n",
    "    idx[0] = i_val\n",
    "\n",
    "    # descend the heap, swapping values until the max heap criterion is met\n",
    "    i = 0\n",
    "    while True:\n",
    "        ic1 = 2 * i + 1\n",
    "        ic2 = ic1 + 1\n",
    "\n",
    "        if ic1 >= size:\n",
    "            break\n",
    "        elif ic2 >= size:\n",
    "            if dist[ic1] > val:\n",
    "                i_swap = ic1\n",
    "            else:\n",
    "                break\n",
    "        elif dist[ic1] >= dist[ic2]:\n",
    "            if val < dist[ic1]:\n",
    "                i_swap = ic1\n",
    "            else:\n",
    "                break\n",
    "        else:\n",
    "            if val < dist[ic2]:\n",
    "                i_swap = ic2\n",
    "            else:\n",
    "                break\n",
    "\n",
    "        dist[i] = dist[i_swap]\n",
    "        idx[i] = idx[i_swap]\n",
    "\n",
    "        i = i_swap\n",
    "\n",
    "    dist[i] = val\n",
    "    idx[i] = i_val\n",
    "\n",
    "    return 0\n",
    "\n",
    "cdef inline void dual_swap(\n",
    "    floating* dist,\n",
    "    integral* idx,\n",
    "    integral i1,\n",
    "    integral i2\n",
    ") nogil:\n",
    "    \"\"\"swap the values at inex i1 and i2 of both dist and idx\"\"\"\n",
    "    cdef:\n",
    "        floating dtmp = dist[i1]\n",
    "        integral itmp = idx[i1]\n",
    "    \n",
    "    dist[i1] = dist[i2]\n",
    "    dist[i2] = dtmp\n",
    "\n",
    "    idx[i1] = idx[i2]\n",
    "    idx[i2] = itmp\n",
    "    \n",
    "cdef int _simultaneous_sort(\n",
    "    floating* dist,\n",
    "    integral* idx,\n",
    "    integral size\n",
    ") nogil except -1:\n",
    "    \"\"\"\n",
    "    Perform a recursive quicksort on the dist array, simultaneously\n",
    "    performing the same swaps on the idx array.\n",
    "    \"\"\"\n",
    "    cdef:\n",
    "        integral pivot_idx, i, store_idx\n",
    "        floating pivot_val\n",
    "\n",
    "    # in the small-array case, do things efficiently\n",
    "    if size <= 1:\n",
    "        pass\n",
    "    elif size == 2:\n",
    "        if dist[0] > dist[1]:\n",
    "            dual_swap(dist, idx, 0, 1)\n",
    "    elif size == 3:\n",
    "        if dist[0] > dist[1]:\n",
    "            dual_swap(dist, idx, 0, 1)\n",
    "        if dist[1] > dist[2]:\n",
    "            dual_swap(dist, idx, 1, 2)\n",
    "            if dist[0] > dist[1]:\n",
    "                dual_swap(dist, idx, 0, 1)\n",
    "    else:\n",
    "        # Determine the pivot using the median-of-three rule.\n",
    "        # The smallest of the three is moved to the beginning of the array,\n",
    "        # the middle (the pivot value) is moved to the end, and the largest\n",
    "        # is moved to the pivot index.\n",
    "        pivot_idx = size / 2\n",
    "        if dist[0] > dist[size - 1]:\n",
    "            dual_swap(dist, idx, 0, size - 1)\n",
    "        if dist[size - 1] > dist[pivot_idx]:\n",
    "            dual_swap(dist, idx, size - 1, pivot_idx)\n",
    "            if dist[0] > dist[size - 1]:\n",
    "                dual_swap(dist, idx, 0, size - 1)\n",
    "        pivot_val = dist[size - 1]\n",
    "\n",
    "        # partition indices about pivot.  At the end of this operation,\n",
    "        # pivot_idx will contain the pivot value, everything to the left\n",
    "        # will be smaller, and everything to the right will be larger.\n",
    "        store_idx = 0\n",
    "        for i in range(size - 1):\n",
    "            if dist[i] < pivot_val:\n",
    "                dual_swap(dist, idx, i, store_idx)\n",
    "                store_idx += 1\n",
    "        dual_swap(dist, idx, store_idx, size - 1)\n",
    "        pivot_idx = store_idx\n",
    "\n",
    "        # recursively sort each side of the pivot\n",
    "        if pivot_idx > 1:\n",
    "            _simultaneous_sort(dist, idx, pivot_idx)\n",
    "        if pivot_idx + 2 < size:\n",
    "            _simultaneous_sort(dist + pivot_idx + 1,\n",
    "                               idx + pivot_idx + 1,\n",
    "                               size - pivot_idx - 1)\n",
    "    return 0\n",
    "\n",
    "### \n",
    "    \n",
    "cdef void _k_closest_chunk(\n",
    "    floating[:, ::1] X_c,            # IN\n",
    "    floating[:, ::1] Y,              # IN\n",
    "    floating[::1] Y_sq_norms,        # IN\n",
    "    floating *dist_middle_terms,     # IN\n",
    "    floating *heap_red_distances,    # IN/OUT\n",
    "    integral *heap_indices,          # IN/OUT\n",
    "    integral k,                      # IN\n",
    "    # ID of the first element of Y_c\n",
    ") nogil:\n",
    "    cdef:\n",
    "        integral i, j\n",
    "    # Instead of computing the full pairwise squared distances matrix,\n",
    "    # ||X_c - Y||² = ||X_c||² - 2 X_c.Y^T + ||Y||², we only need\n",
    "    # to store the - 2 X_c.Y^T + ||Y||² term since the argmin\n",
    "    # for a given sample X_c^{i} does not depend on ||X_c^{i}||²\n",
    "            \n",
    "    # Careful: LDA, LDB and LDC are given for F-ordered arrays.\n",
    "    # Here, we use their counterpart values as indicated in the documentation.\n",
    "    # See the documentation of parameters here:\n",
    "    # https://www.netlib.org/lapack/explore-html/db/dc9/group__single__blas__level3_gafe51bacb54592ff5de056acabd83c260.html\n",
    "    #\n",
    "    # dist_middle_terms = -2 * X_c.dot(Y_c.T)\n",
    "    _gemm(RowMajor, NoTrans, Trans,\n",
    "          X_c.shape[0], Y.shape[0], X_c.shape[1],\n",
    "          -2.0,\n",
    "          &X_c[0, 0], X_c.shape[1],\n",
    "          &Y[0, 0], X_c.shape[1], 0.0,\n",
    "          dist_middle_terms, Y.shape[0])\n",
    "    \n",
    "    # Computing argmins here\n",
    "    for i in range(X_c.shape[0]):\n",
    "        for j in range(Y.shape[0]):\n",
    "            _push(heap_red_distances + i * k,\n",
    "                  heap_indices + i * k,\n",
    "                  k,\n",
    "                  # reduced distance: - 2 X_c_i.Y_c_j^T + ||Y_c_j||²\n",
    "                  dist_middle_terms[i * Y.shape[0] + j] + Y_sq_norms[j],\n",
    "                  j)\n",
    "            \n",
    "                \n",
    "                \n",
    "cdef int _pairwise_k_closest(\n",
    "    floating[:, ::1] X,              # IN\n",
    "    floating[:, ::1] Y,              # IN\n",
    "    floating[::1] Y_sq_norms,        # IN\n",
    "    integral chunk_size,\n",
    "    integral[:, ::1] Z_idx,          # OUT\n",
    ") nogil except -1:\n",
    "    cdef:\n",
    "        integral n_samples_chunk = chunk_size / X.shape[1]\n",
    "        \n",
    "        integral X_n_samples_chunk = min(X.shape[0], n_samples_chunk)\n",
    "        integral X_n_full_chunks = X.shape[0] // X_n_samples_chunk\n",
    "        integral X_n_samples_rem = X.shape[0] % X_n_samples_chunk\n",
    "        \n",
    "        # Counting remainder chunk in total number of chunks\n",
    "        integral X_n_chunks = X_n_full_chunks + (X.shape[0] != (X_n_full_chunks * X_n_samples_chunk))\n",
    "        \n",
    "        integral n_chunks = X_n_chunks\n",
    "        integral num_threads = min(n_chunks, N_THREADS)\n",
    "\n",
    "        integral idx = 0\n",
    "        integral j = 0\n",
    "        integral i_chunk = 0\n",
    "        integral k = Z_idx.shape[1]\n",
    "        \n",
    "        integral X_start, X_end\n",
    "        integral X_chunk_idx \n",
    "        floating *dist_middle_terms\n",
    "\n",
    "    IF DEBUG:\n",
    "        printf(\"Using %ld chunk pairs\\nUsing %ld threads\\n\", n_chunks, num_threads)\n",
    "    \n",
    "    with nogil, parallel(num_threads=num_threads):\n",
    "        # Thread local buffers\n",
    "        \n",
    "        # Temporary buffer for the -2 * X_c.dot(Y_c.T) term\n",
    "        dist_middle_terms = <floating*> malloc(Y.shape[0] * X_n_samples_chunk * sizeof(floating))\n",
    "        heap_red_distances = <floating*> malloc(X_n_samples_chunk * k * sizeof(floating))\n",
    "        \n",
    "        for X_chunk_idx in prange(X_n_chunks, schedule='static'):\n",
    "            # We reset the heap between X chunks\n",
    "            memset(heap_red_distances, <integral> INF, X_n_samples_chunk * k * sizeof(floating))\n",
    "            \n",
    "            X_start = X_chunk_idx * X_n_samples_chunk\n",
    "            if X_chunk_idx == X_n_chunks - 1 and X_n_samples_rem > 0:\n",
    "                X_end = X_start + X_n_samples_rem\n",
    "            else:\n",
    "                X_end = X_start + X_n_samples_chunk\n",
    "\n",
    "            _k_closest_chunk(\n",
    "                X[X_start:X_end, :],\n",
    "                Y,\n",
    "                Y_sq_norms,\n",
    "                dist_middle_terms,\n",
    "                heap_red_distances,\n",
    "                &Z_idx[X_start, 0],\n",
    "                k\n",
    "            )\n",
    "            \n",
    "            # Getting the indices of the k-closest points in\n",
    "            # the sorted order\n",
    "            for idx in range(X_end - X_start):\n",
    "                _simultaneous_sort(\n",
    "                    heap_red_distances + idx * k,\n",
    "                    &Z_idx[X_start + idx, 0],\n",
    "                    k\n",
    "                )\n",
    "                           \n",
    "        # end: for X_chunk_idx\n",
    "        free(dist_middle_terms)\n",
    "        free(heap_red_distances)\n",
    "    \n",
    "    # end: with nogil, parallel\n",
    "    IF DEBUG:\n",
    "        printf(\"Done cdef _pairwise_k_closest \\n\\n\")\n",
    "        \n",
    "# Python-accessible interfaces\n",
    "def pairwise_argmin(\n",
    "    floating[:, ::1] X,\n",
    "    floating[:, ::1] Y,\n",
    "    integral k,\n",
    "    integral chunk_size = CHUNK_SIZE,\n",
    "):\n",
    "    int_dtype = np.int32 if integral is int else np.int64\n",
    "    float_dtype = np.float32 if floating is float else np.float64\n",
    "    cdef:\n",
    "        integral[:, ::1] Z_idx = np.full((X.shape[0], k), -1, dtype=int_dtype)\n",
    "        floating[::1] Y_sq_norms = np.einsum('ij,ij->i', Y, Y)\n",
    "    \n",
    "    _pairwise_k_closest(X, Y, Y_sq_norms, chunk_size, Z_idx)\n",
    "    \n",
    "    return np.asarray(Z_idx)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "marine-darwin",
   "metadata": {},
   "outputs": [],
   "source": [
    "X_train = np.random.rand(int(20 * 2)).reshape((-1, 2))\n",
    "X_test = np.copy(X_train[::2, :]) + 0.02"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "injured-alert",
   "metadata": {},
   "outputs": [],
   "source": [
    "X_train.nbytes / 1e9, X_test.nbytes /1e9"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "human-prefix",
   "metadata": {},
   "outputs": [],
   "source": [
    "argmins = pairwise_argmin(X_test, X_train, chunk_size = 100, k=1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "adapted-plant",
   "metadata": {},
   "outputs": [],
   "source": [
    "argmins"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "numerous-belief",
   "metadata": {},
   "outputs": [],
   "source": [
    "_ = plt.scatter(X_test[:,0], X_test[:,1])\n",
    "_ = plt.scatter(X_train[:,0], X_train[:,1])\n",
    "_ = plt.legend([\"X_test\", \"X_train\"])\n",
    "\n",
    "for i, j in enumerate(argmins):\n",
    "     _ = plt.plot((X_test[i, 0], X_train[j, 0]),\n",
    "             (X_test[i, 1], X_train[j, 1]),\n",
    "             'g--', linewidth=0.5)\n",
    "    \n",
    "_ = plt.title(\"Argmin for X_test on X_train\")\n",
    "\n",
    "plt.show()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "directed-pharmacy",
   "metadata": {},
   "source": [
    "### Testing for correctness against scikit-learn"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "demonstrated-superintendent",
   "metadata": {},
   "outputs": [],
   "source": [
    "@pytest.mark.parametrise(\"n\", [10 ** i for i in [2, 3, 4]])\n",
    "@pytest.mark.parametrise(\"d\", [2, 5, 10, 100])\n",
    "def test_correctness(n, d):\n",
    "    np.random.seed(1)\n",
    "    Y = np.random.rand(int(n * d)).reshape((-1, d))\n",
    "    X = np.random.rand(int(n * d // 2)).reshape((-1, d))\n",
    "        \n",
    "    neigh = NearestNeighbors(n_neighbors=1, algorithm='brute')\n",
    "    neigh.fit(Y)\n",
    "    \n",
    "    argmins_sk = neigh.kneighbors(X, return_distance=False)\n",
    "    print(\"Done with neigh.kneighbors\")\n",
    "    argmins = pairwise_argmin(X, Y, k=1)\n",
    "    print(\"Done with pairwise_argmin\")\n",
    "\n",
    "    np.testing.assert_array_equal(np.ndarray.flatten(argmins), np.ndarray.flatten(argmins_sk))\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "grateful-marine",
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "for n, d in itertools.product([10 ** i for i in [2, 3, 4]], [2, 5, 10, 100]):\n",
    "    print(n, d)\n",
    "    test_correctness(n, d)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "arabic-trace",
   "metadata": {},
   "source": [
    "### Comparison against scikit-learn"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "equipped-empty",
   "metadata": {},
   "outputs": [],
   "source": [
    "Y = np.random.rand(int(1e7)).reshape((-1, 500))\n",
    "X = np.copy(Y[::2, :]) + 0.02\n",
    "k = 100"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "thick-external",
   "metadata": {},
   "outputs": [],
   "source": [
    "X.nbytes / 1e9, Y.nbytes /1e9"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "short-contrast",
   "metadata": {},
   "outputs": [],
   "source": [
    "X.shape, Y.shape"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "relative-suggestion",
   "metadata": {},
   "outputs": [],
   "source": [
    "neigh = NearestNeighbors(n_neighbors=k, algorithm='brute').fit(Y)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ecological-israel",
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit neigh.kneighbors(X, return_distance=False)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "annoying-credits",
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit pairwise_argmin(X, Y, k=k, chunk_size=4096 // 2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "multiple-difficulty",
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit pairwise_argmin(X, Y, k=k, chunk_size=4096)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "equivalent-board",
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit pairwise_argmin(X, Y, k=k, chunk_size=4096 * 2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "seven-glasgow",
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit pairwise_argmin(X, Y, k=k, chunk_size=4096 * 3)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fifty-telescope",
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit pairwise_argmin(X, Y, k=k, chunk_size=4096 * 4)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "boring-cleaners",
   "metadata": {},
   "outputs": [],
   "source": [
    "patch_sklearn()\n",
    "neigh = NearestNeighbors(n_neighbors=k, algorithm='brute').fit(Y)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "immune-isaac",
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit neigh.kneighbors(X, return_distance=False)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "local-syndication",
   "metadata": {},
   "outputs": [],
   "source": [
    "unpatch_sklearn()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "general-fisher",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
